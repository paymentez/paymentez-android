apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    bintrayRepo = 'Paymentez'
    bintrayName = 'paymentez-android'

    publishedGroupId = 'com.paymentez'
    libraryName = 'paymentez-android'
    artifact = 'paymentez-android'

    libraryDescription = 'Paymentez Android SDK is a library that allows developers to easily connect to the Paymentez CREDITCARDS API'

    siteUrl = 'https://github.com/paymentez/paymentez-android-example'
    gitUrl = 'https://github.com/paymentez/paymentez-android-example.git'

    libraryVersion = '1.0.1'

    developerId = 'paymentez'
    developerName = 'Paymentez'
    developerEmail = 'dev@paymentez.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

group = publishedGroupId
version = libraryVersion


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'io.card:android-sdk:5.5.0'
    compile 'commons-codec:commons-codec:1.10'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    compile 'com.google.code.gson:gson:2.6.1'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name libraryName
            description libraryDescription
            url siteUrl

            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        version {
            name = libraryVersion
            desc = libraryDescription
            vcsTag = libraryVersion
        }
    }
}