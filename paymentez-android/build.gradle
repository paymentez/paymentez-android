apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    bintrayRepo = 'Paymentez'
    bintrayName = 'paymentez-android'

    publishedGroupId = 'com.paymentez'
    libraryName = 'paymentez-android'
    artifact = 'paymentez-android'

    libraryDescription = 'Paymentez Android SDK is a library that allows developers to easily connect to the Paymentez CREDITCARDS API'

    siteUrl = 'https://github.com/paymentez/paymentez-android'
    gitUrl = 'https://github.com/paymentez/paymentez-android.git'

    libraryVersion = '1.3.1'

    developerId = 'paymentez'
    developerName = 'Paymentez'
    developerEmail = 'dev@paymentez.com'

    licenseName = 'MIT'
    licenseUrl = 'https://github.com/paymentez/paymentez-android/blob/master/LICENSE'
    allLicenses = ["MIT"]
}

group = publishedGroupId
version = libraryVersion

android {
    compileSdkVersion 23
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 6
        versionName libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        disable 'ValidFragment'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:23.4.0'
    implementation 'com.android.support:design:23.4.0'
    implementation 'io.card:android-sdk:5.5.0'
    implementation 'commons-codec:commons-codec:1.10'
    api 'com.squareup.retrofit2:retrofit:2.6.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.12.0'
    api 'com.google.code.gson:gson:2.8.5'
    api 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name libraryName
            description libraryDescription
            url siteUrl

            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = user
        licenses = allLicenses
        vcsUrl = gitUrl
        version {
            name = libraryVersion
            desc = libraryDescription
            vcsTag = libraryVersion
        }
    }
}